def scalaMajorVersion = gradle.ext.scalaMajorVersion
def scalaMinorVersion = gradle.ext.scalaMinorVersion

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://dl.bintray.com/ngbinh/maven"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.hierynomus.gradle.plugins:license-gradle-plugin:0.16.1"
        classpath "org.owasp:dependency-check-gradle:9.1.0"
    }

    ext.akkaVersion = '2.6.20'
    ext.awsSdkVersion = '1.12.770'
    ext.comFasterxmlJacksonVersion = '2.14.1'
    ext.orgScalatestVersion = '3.2.15'
}

apply plugin: "java-library"
apply plugin: "scala"
apply plugin: "maven-publish"
apply plugin: "application"
apply plugin: "com.github.hierynomus.license"
apply plugin: "signing"
apply plugin: "org.owasp.dependencycheck"

group = "com.sumologic.sumobot"
description = "A Slack bot implemented in Akka"

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '3.0.3-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    api "com.github.slack-scala-client:slack-scala-client_${scalaMajorVersion}:0.4.4"
    api "com.typesafe.akka:akka-testkit_${scalaMajorVersion}:${akkaVersion}"
    api "com.typesafe.akka:akka-stream_${scalaMajorVersion}:${akkaVersion}"

    implementation("com.offbytwo.jenkins:jenkins-client:0.3.8") {
        exclude group: 'commons-beanutils', module: 'commons-beanutils'
        exclude group: 'dom4j', module: 'dom4j'
    }
    implementation('commons-beanutils:commons-beanutils:1.9.4')
    implementation("org.apache.httpcomponents:httpclient") {
      version {
        strictly("4.5.14")
      }
    }
    implementation "org.scalatra.scalate:scalate-core_${scalaMajorVersion}:1.10.1"
    implementation "org.slf4j:slf4j-log4j12:2.0.13"
    implementation "com.amazonaws:aws-java-sdk-support:${awsSdkVersion}"
    implementation "com.amazonaws:aws-java-sdk-s3:${awsSdkVersion}"
    implementation("org.quartz-scheduler:quartz:2.3.2") {
        exclude group: "c3p0", module: "c3p0"
    }
    implementation "com.mchange:c3p0:0.9.5.5"
    implementation("net.liftweb:lift-json_${scalaMajorVersion}:3.4.1") {
        exclude group: "org.specs2", module: "specs2_${scalaMajorVersion}"
    }
    implementation "org.scala-lang:scala-compiler:${scalaMajorVersion}.${scalaMinorVersion}"
    implementation "org.scala-lang.modules:scala-collection-compat_${scalaMajorVersion}:2.6.0"

    implementation "com.fasterxml.jackson.core:jackson-databind:${comFasterxmlJacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${comFasterxmlJacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${comFasterxmlJacksonVersion}"
    implementation "org.dom4j:dom4j:2.1.4"
    implementation "commons-beanutils:commons-beanutils:1.9.4"
    api "com.typesafe.play:play-json_${scalaMajorVersion}:2.9.2"
    api "com.pauldijou:jwt-play-json_${scalaMajorVersion}:3.1.0"
    api "com.typesafe.akka:akka-http_${scalaMajorVersion}:10.2.10"
    implementation "org.joda:joda-convert:2.2.3"

    implementation "junit:junit:4.13.2"
    implementation "org.scalatest:scalatest_${scalaMajorVersion}:$orgScalatestVersion"
    implementation "org.scalatest:scalatest-wordspec_${scalaMajorVersion}:$orgScalatestVersion"
    implementation "org.scalatest:scalatest-shouldmatchers_${scalaMajorVersion}:$orgScalatestVersion"
    implementation "org.scalatestplus:junit-4-13_${scalaMajorVersion}:3.2.15.0"
    implementation "org.scalatestplus:mockito-4-6_${scalaMajorVersion}:3.2.15.0"
    implementation "org.mockito:mockito-core:4.11.0"
}

compileScala {
    scalaCompileOptions.additionalParameters = [
        "-feature", "-target:jvm-1.8", "-deprecation", "-language:implicitConversions",
        "-unchecked"
        // , "-Xfatal-warnings" // NOTE(chris, 2020-07-03): As much as I'd like fatal-warnings, it's basically impossible with 2.12 and 2.13
    ]
}

application {
    mainClassName = 'com.sumologic.sumobot.Main'
    applicationDefaultJvmArgs = ['-Dconfig.override_with_env_vars=true']
}

ext.gitCommitIdAbbrev = System.getenv('GIT_COMMIT_ID_ABBREV') ?: 'git rev-parse --verify --short=12 HEAD'.execute().text.trim()

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from(sourceSets.main.allJava) {
        expand(project: [version: project.version], git: [commit: [id: [abbrev: gitCommitIdAbbrev]]])
    }
    from(sourceSets.main.allScala)
}

task javadocJar(type: Jar, dependsOn: scaladoc) {
    from javadoc
    from scaladoc
    archiveClassifier.set('javadoc')
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId(project.group)
            artifactId(project.name + "_${scalaMajorVersion}")
            version(project.version)

            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/SumoLogic/sumobot'

                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'goledzki'
                        name = 'Greg Oledzki'
                        organization = 'Sumo Logic'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:SumoLogic/sumobot.git'
                    developerConnection = 'scm:git:git@github.com:SumoLogic/sumobot.git'
                    url = 'https://github.com/SumoLogic/sumobot'
                    tag = 'HEAD'
                }
            }

            from(components.java)
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }
    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username project.findProperty("nexus_username") ?: ""
                password project.findProperty("nexus_password") ?: ""
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.maven
}

license {
    header rootProject.file('src/etc/header.txt')
    strictCheck true
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencyCheck {
    format = 'ALL'
    suppressionFile = 'project-suppression.xml'
}

test {
    testLogging {
        exceptionFormat 'full'
        showExceptions true
        showStackTraces true
    }
}